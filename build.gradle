apply plugin:'java'
apply plugin:'eclipse'
apply plugin:'application'

mainClassName = "hu.rxd.filebot.ScanBot"

sourceSets {
	main {
		java.srcDir 'source'
		resources.srcDir 'source'
	}
	test {
		java.srcDir 'test'
		resources.srcDir 'test'
	}
}

repositories{
	mavenCentral()
}

dependencies {

	def x=new XmlSlurper().parseText(new File('ivy.xml').text)
	x.dependencies.dependency.each {  dep ->
		compile "${dep.@org}:${dep.@name}:${dep.@rev}"
	}
	fileTree('lib/jars').files.each { f -> 
		compile files(f)
	}
}

   eclipse.classpath.file.withXml {XmlProvider provider ->
	def libPath = file('lib/native/linux-amd64/')
         provider.asNode().classpathentry.findAll{it.'@path'.contains('net.sf.sevenzipjbinding')}
          .each {
             it.appendNode('attributes')
              .appendNode('attribute', [name:'org.eclipse.jdt.launching.CLASSPATH_ATTR_LIBRARY_PATH_ENTRY', value:libPath])
            }
     }
